# SV
model {
    # Priors
    mu ~ dnorm(0., 0.04)
    beta <- exp(.5 * mu)
    phistar ~ dbeta(20., 1.5)
    phi <- 2. * phistar - 1.
    recsigmav2 ~ dgamma(2.5, 0.025)
    sigmav <- sqrt(1. / recsigmav2)
    alpha <- (1. - phi) * mu

    # Initial distribution of the state
    x0 ~ dnorm(mu, recsigmav2)

    # Transition density
    xmean[1] <- alpha + phi * x0
    x[1] ~ dnorm(xmean[1], recsigmav2)I(-5,5)
    for (t in 2:n) {
        xmean[t] <- alpha + phi * x[t-1]
        x[t] ~ dnorm(xmean[t], recsigmav2)I(-4,4)
    }

    # Likelihood
    for (t in 1:n) {
        recyvar[t] <- 1. / exp(x[t])
        y[t] ~ dnorm(0, recyvar[t])
    }
}

# Initial values
list(mu=0, phistar=0.98, recsigmav2=40)


# SVL

model {
    # Priors
    mu ~ dnorm(0., 0.04)
    beta <- exp(.5 * mu)
    phistar ~ dbeta(20., 1.5)
    phi <- 2. * phistar - 1.
    recsigmav2 ~ dgamma(2.5, 0.025)
    sigmav <- sqrt(1. / recsigmav2)
    rho ~ dunif(-1., 1.)
    alpha <- (1. - phi) * mu

    # Initial distribution of the state
    x0 ~ dnorm(mu, recsigmav2)
    xmean[1] <- alpha + phi * x0
    x[1] ~ dnorm(xmean[1], recsigmav2)I(-5,5)

    # Transition density
    for (t in 1:n) {
        xmean[t+1] <- alpha + phi * x[t]
        x[t+1] ~ dnorm(xmean[t+1], recsigmav2)I(-4,4)
    }

    # Likelihood
    for (t in 1:n) {
        ymean[t] <- rho/sigmav * exp(0.5 * x[t]) * (x[t+1] - alpha - phi * x[t])
        recyvar[t] <- 1. / ( exp(x[t]) * (1 - rho*rho) )
        y[t] ~ dnorm(ymean[t], recyvar[t])
    }
}

# Initial values
list(mu=0, phistar=0.98, recsigmav2=40, rho=-0.4)


# SVL2

model {
    # Priors
    mu ~ dnorm(0., 0.04)
    beta <- exp(.5 * mu)
    phistar ~ dbeta(20, 1.5)
    phi <- 2*phistar - 1
    recsigmav2 ~ dgamma(2.5, 0.025)
    sigmav <- sqrt(1/recsigmav2)
    rho ~ dunif(-1, 1)
    alpha <- mu*(1-phi)

    # Initial distribution of the state
    x0 ~ dnorm(mu, recsigmav2)
    xmean[1] <- alpha + phi * x0
    x[1] ~ dnorm(xmean[1], recsigmav2)I(-5,5)

    # Transition density
    for (t in 2:n) {
        xmean[t] <- alpha + phi * x[t-1]
        x[t] ~ dnorm(xmean[t], recsigmav2)I(-4,4)
    }

    # Likelihood
    ymean[1] <- rho/sigmav * exp(0.5 * x[1]) * (x[1] - alpha - phi * x0 - 0.5*sigmav*sigmav)
    recyvar[1] <- 1. / ( exp(x[1]) * (1 - rho*rho) )
    y[1] ~ dnorm(ymean[1], recyvar[1])
    for (t in 2:n) {
        ymean[t] <- rho/sigmav * exp(0.5 * x[t]) * (x[t] - alpha - phi*x[t-1] - 0.5*sigmav*sigmav)
        recyvar[t] <- 1. / ( exp(x[t]) * (1 - rho*rho) )
        y[t] ~ dnorm(ymean[t], recyvar[t])
    }
}

# Initial values
list(mu=0, phistar=0.98, recsigmav2=40, rho=-0.4)


# wcSVL

model {
    # Priors
    mu ~ dnorm(0., 0.04)
    beta <- exp(.5 * mu)
    phistar ~ dbeta(20., 1.5)
    phi <- 2. * phistar - 1.
    recsigmav2 ~ dgamma(2.5, 0.025)
    sigmav <- sqrt(1. / recsigmav2)
    rho ~ dunif(-1., 1.)
    oneminusphi <- 1. - phi
    alpha <- oneminusphi * mu

    # Initial distribution of the state
    x0 ~ dnorm(mu, recsigmav2)

    # We assume that dt[0] == dt[1] as a corner case
    xmean[1] <- dt[1] * alpha + (1. - dt[1] * oneminusphi) * x0
    recxvar[1] <- recsigmav2 / dt[1]
    x[1] ~ dnorm(xmean[1], recxvar[1])I(-5,5)

    # Transition density
    for (t in 1:n) {
        xmean[t+1] <- dt[t] * alpha + (1. - dt[t] * oneminusphi) * x[t]
        recxvar[t+1] <- recsigmav2 / dt[t]
        x[t+1] ~ dnorm(xmean[t+1], recxvar[t+1])I(-4,4)
    }

    # Likelihood
    for (t in 1:n) {
        ymean[t] <- sqrt(dt[t]) * exp(0.5 * x[t]) * rho / (sqrt(dt[t]) * sigmav) * (x[t+1] - dt[t] * alpha - (1. - dt[t] * oneminusphi) * x[t])
        recyvar[t] <- 1. / ( dt[t] * exp(x[t]) * (1. - rho*rho) )
        y[t] ~ dnorm(ymean[t], recyvar[t])
    }
}

# Initial values
list(mu=0, phistar=0.98, recsigmav2=40, rho=-0.4)
